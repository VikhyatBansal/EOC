class Main {
    function void main(){
      var int i, length;
      var Array inputArray;

      let length = Keyboard.readInt("How many numbers? ");
      let inputArray = Array.new(length);

      let i = 0;

      while(i < length){
        let inputArray[i] = Keyboard.readInt("Enter a number: ");
        let i = i + 1;
      }

      do Main.mergeSort(inputArray, length);

      // To print the processed array
      let i = 0;
      while(i < length){
        do Output.printInt(inputArray[i]);
        if(i<(length-1)){
          do Output.printString(",");
        }
        let i = i + 1;
      }

      return;
    }

    function void mergeSort(Array inputArray, int inputLength){
    var int i;
    var int middleIndex;
    var Array leftHalf;
    var Array rightHalf;
    var int rightSize;

    // Check if the inputLength is less than two
    // As arrays with 1 element is already sorted

      if(inputLength < 2){
        return;
      }

    // Get the midpoint of the array

    let middleIndex = inputLength/2;
    let rightSize = inputLength-middleIndex;
    // Split the arrays

    let leftHalf = Array.new(middleIndex);
    let rightHalf = Array.new(rightSize);

    // Populating the leftHalf array
    let i = 0;
    while(i < middleIndex){
      let leftHalf[i] = inputArray[i];
      let i = i + 1;
    }

    // Populating the rightHalf
    let i = middleIndex;
    while(i < inputLength){
      let rightHalf[i - middleIndex] = inputArray[i];
      let i = i + 1;
    }

      // Recursive call to sort arrays
      do Main.mergeSort(leftHalf, middleIndex);
      do Main.mergeSort(rightHalf, rightSize);

      do Main.mergeArray(inputArray, leftHalf, rightHalf, middleIndex, rightSize);
      return;
    }
    function boolean gt(int a, int b){
      if(a=b){
      return true;
      }
      if(a<b){
      return true;
      }
      else {
      return false;
      }
    }

    function boolean ands(int lf, int rf, int j, int i){
      if(i < lf){
        if(j < rf){
          return true;
        }
      }
      return false;
    }

    function void mergeArray(Array inputArray, Array leftHalf, Array rightHalf, int leftSize, int rightSize){
     var int i, j, k;
     let i = 0;
     let j = 0;
     let k = 0;

     while (Main.ands(leftSize, rightSize, j, i)){
         if(Main.gt(leftHalf[i], rightHalf[j])){
           let inputArray[k] = leftHalf[i];
           let i = i + 1;
       }
       else{
         let inputArray[k] = rightHalf[j];
         let j = j + 1;
       }
       let k = k + 1;
     }

     while(i < leftSize){
      let inputArray[k] = leftHalf[i];
      let i = i + 1;
      let k = k + 1;
     }

     while(j < rightSize){
      let inputArray[k] = rightHalf[j];
      let j = j + 1;
      let k = k + 1;
     }
     return;
    }
}
